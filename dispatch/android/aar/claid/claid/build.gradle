buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        // The Android Gradle Plugin knows how to build native code with the NDK.
        classpath 'com.android.tools.build:gradle:7.3.0'
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.9.4"
    }
}

plugins {
    id 'com.android.library'
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'ch.claid'
                artifactId = 'claid'
                version = '0.6.1'

                from components.release



                pom {
                    packaging = "aar"
                    name.set("CLAIDLib")
                    description.set("CLAID: Closing the Loop on AI & Data Collection")
                    url.set("https://github.com/ADAMMA-CDHI-ETH-Zurich/CLAID")
                    inceptionYear.set("2023")

                    licenses {
                        license {
                            name.set("Apache2 License")
                            url.set("https://www.apache.org/licenses/LICENSE-2.0")
                        }
                    }

                    developers {
                        developer {
                            id.set("LastChance")
                            name.set("Patrick Langer")
                            email.set("planger@ethz.ch")
                        }
                    }

                    scm {
                        connection.set("scm:git@github.com:ADAMMA-CDHI-ETH-Zurich/CLAID")
                        developerConnection.set("scm:git@github.com:ADAMMA-CDHI-ETH-Zurich/CLAID.git")
                        url.set("https://github.com/ADAMMA-CDHI-ETH-Zurich/CLAID.git")
                    }

                }
            }
        }
    }
}


apply plugin: 'com.google.protobuf'


android {
    namespace 'adamma.c4dhi.claid'
    compileSdk 33

    defaultConfig {
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "0.6.1"

        aarMetadata {
            minCompileSdk = 26
        }


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }



    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets{

        main.java.srcDirs += ["../../../platform"]
        main.java.srcDirs += ["../../../../java/runtime"]
        main.proto.srcDirs += "../../../../proto"
        main.jniLibs.srcDirs = ['../blobs/']
        main.resources.srcDirs += ['src/main/xml']

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.24.0' }
    plugins {
        grpc { artifact = 'io.grpc:protoc-gen-grpc-java:1.59.0' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { }
            }
            task.plugins {
                grpc { // Options added to --grpc_out
                }
            }
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.work:work-runtime:2.8.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    api "com.google.protobuf:protobuf-java:3.24.0"
    api "com.google.protobuf:protobuf-java-util:3.24.0"
    api 'io.grpc:grpc-protobuf:1.59.0' // CURRENT_GRPC_VERSION
    api 'io.grpc:grpc-stub:1.59.0' // CURRENT_GRPC_VERSION
    api 'io.grpc:grpc-android:1.59.0' // CURRENT_GRPC_VERSION
    api 'io.grpc:grpc-okhttp:1.59.0' // CURRENT_GRPC_VERSION
    api 'javax.annotation:javax.annotation-api:1.3.2'

}