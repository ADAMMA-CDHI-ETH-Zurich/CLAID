// The Android Gradle Plugin builds the native code with the Android NDK.

group 'com.example.claid_core'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        // The Android Gradle Plugin knows how to build native code with the NDK.
        classpath 'com.android.tools.build:gradle:7.3.0'
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.9.4"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

apply plugin: 'com.google.protobuf'


android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.example.claid_core'
    }

    // Bumping the plugin compileSdkVersion requires all clients of this plugin
    // to bump the version in their app.
    compileSdkVersion 33

    // Bumping the plugin ndkVersion requires all clients of this plugin to bump
    // the version in their app and to download a newer version of the NDK.
    ndkVersion "21.4.7075529"

    // Invoke the shared CMake build with the Android Gradle Plugin.
    externalNativeBuild {
        cmake {
            path "../src/CMakeLists.txt"

            // The default CMake version for the Android Gradle Plugin is 3.10.2.
            // https://developer.android.com/studio/projects/install-ndk#vanilla_cmake
            //
            // The Flutter tooling requires that developers have CMake 3.10 or later
            // installed. You should not increase this version, as doing so will cause
            // the plugin to fail to compile for some customers of the plugin.
            // version "3.10.2"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 21
    }

    sourceSets {

        main.proto.srcDirs += "../../../proto"
        
    }
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.24.0' }
    plugins {
        grpc { artifact = 'io.grpc:protoc-gen-grpc-java:1.59.0' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { }
            }
            task.plugins {
                grpc { // Options added to --grpc_out
                    }
            }
        }
    }
}

dependencies
{
    implementation 'io.grpc:grpc-protobuf:1.59.0' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-stub:1.59.0' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-android:1.59.0' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-okhttp:1.59.0' // CURRENT_GRPC_VERSION
    implementation 'javax.annotation:javax.annotation-api:1.3.2' 
}
