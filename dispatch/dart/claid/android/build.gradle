// The Android Gradle Plugin builds the native code with the Android NDK.

group 'ch.claid.flutter'
version '0.6.2'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        // The Android Gradle Plugin knows how to build native code with the NDK.
        classpath 'com.android.tools.build:gradle:7.3.0'
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.9.4"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()

        flatDir {
            dirs project(':claid').file('libs')
            // e.g. dirs project(':url_launcher').file('libs')  - don't miss the ':'
        }
    }

    
}

apply plugin: 'com.android.library'

apply plugin: 'com.google.protobuf'


android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.example.claid_core'
    }

    // Bumping the plugin compileSdkVersion requires all clients of this plugin
    // to bump the version in their app.
    compileSdkVersion 33

    // Bumping the plugin ndkVersion requires all clients of this plugin to bump
    // the version in their app and to download a newer version of the NDK.
    ndkVersion "21.4.7075529"

    // Invoke the shared CMake build with the Android Gradle Plugin.
    externalNativeBuild {
        cmake {
            path "../src/CMakeLists.txt"

            // The default CMake version for the Android Gradle Plugin is 3.10.2.
            // https://developer.android.com/studio/projects/install-ndk#vanilla_cmake
            //
            // The Flutter tooling requires that developers have CMake 3.10 or later
            // installed. You should not increase this version, as doing so will cause
            // the plugin to fail to compile for some customers of the plugin.
            // version "3.10.2"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 21
    }

    sourceSets {
        main.proto.srcDirs += "src/proto"
        main.jniLibs.srcDirs = ['../blobs/android']
    }



    buildTypes {
        release {
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
        debug {
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.24.0' }
    plugins {
        grpc { artifact = 'io.grpc:protoc-gen-grpc-java:1.59.0' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { }
            }
            task.plugins {
                grpc { // Options added to --grpc_out
                    }
            }
        }
    }
}

dependencies
{
    api "ch.claid:claid:0.6.2"
}

