plugins {
    id 'com.android.application'
}

android {
    namespace 'adamma.c4dhi.claid.wearos'
    compileSdk 33

    defaultConfig {
        applicationId "adamma.c4dhi.claid.wearos"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.18.1'
        }
    }

    sourceSets {
        if (file('src/main/cpp/CLAIDJavaDirectories.txt').exists()) {
            def srcDirs = file('src/main/cpp/CLAIDJavaDirectories.txt').text.readLines()
            srcDirs.each {
                srcDirectory ->
                    main {
                        java {
                            srcDir srcDirectory
                        }
                    }
            }
        }

        main.java.srcDirs += "src/main/cpp/JavaCLAID"
        // let gradle pack the shared library into apk
    }
}

task deploy_jbind11_sources(type:Exec){
    print("$projectDir/src/main/cpp/JavaCLAID")
    workingDir "$projectDir/src/main/cpp/JavaCLAID"
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmake', '.', '&&', 'cmake', '--build', '.'
    } else {
        commandLine 'cmake', '.'
        commandLine 'cmake', '--build', '.', '-j'
    }
}



project.afterEvaluate {
    // Specify that java can only be built after native built is complete.
    // That's necessary, as native build might deploy java files generated
    // by jbind11.
    if (tasks.findByName("externalNativeBuildDebug")) {
        compileDebugJavaWithJavac.dependsOn externalNativeBuildDebug
    }
    if (tasks.findByName("externalNativeBuildRelease")) {
        compileReleaseJavaWithJavac.dependsOn externalNativeBuildRelease
    }
}


dependencies {

    implementation 'com.google.android.gms:play-services-wearable:18.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'androidx.wear:wear:1.3.0'
}